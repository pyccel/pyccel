// TODO: - linear:   improve using lists. see specs
//       - parallel: add if parallel

Openmp:
  statements*=OpenmpStmt
;

OpenmpStmt:
  '#$' 'omp' stmt=OmpConstructOrDirective
;

////////////////////////////////////////////////////
//         Constructs and Directives
////////////////////////////////////////////////////
OmpConstructOrDirective:
    OmpParallelConstruct
  | OmpLoopConstruct
  | OmpSingleConstruct
  | OmpEndClause
  | OmpCriticalConstruct
  | OmpBarrierConstruct
  | OmpMasterConstruct
  | OmpMaskedConstruct
  | OmpTaskLoopConstruct
;
////////////////////////////////////////////////////

////////////////////////////////////////////////////
//     Constructs and Directives definitions
////////////////////////////////////////////////////
OmpParallelConstruct: 'parallel'  combined*=ParallelCombined? clauses*=OmpParallelClause;
OmpLoopConstruct:     'for'       clauses*=OmpLoopClause;
OmpTaskLoopConstruct: 'taskloop'  clauses*=OmpTaskLoopClause;
OmpSingleConstruct:   'single'    clauses*=OmpSingleClause;
OmpCriticalConstruct: 'critical'  clauses*=OmpCriticalClause;
OmpMaskedConstruct:   name='masked'  clauses*=OmpMaskedClause;
OmpBarrierConstruct:  name='barrier';
OmpMasterConstruct:   name='master';
////////////////////////////////////////////////////

////////////////////////////////////////////////////
//         Combined Constructs
////////////////////////////////////////////////////
ParallelCombined: ('for' 'simd'? | 'sections');
////////////////////////////////////////////////////

////////////////////////////////////////////////////
//      Clauses for Constructs and Directives
////////////////////////////////////////////////////

OmpParallelClause:
    OmpNumThread
  | OmpDefault
  | OmpPrivate
  | OmpShared
  | OmpFirstPrivate
  | OmpCopyin
  | OmpReduction
  | OmpProcBind
;

OmpLoopClause:
    OmpPrivate
  | OmpFirstPrivate
  | OmpLastPrivate
  | OmpLinear
  | OmpReduction
  | OmpSchedule
  | OmpCollapse
  | OmpOrdered
;

OmpTaskLoopClause:
    OmpShared
  | OmpPrivate
  | OmpFirstPrivate
  | OmpLastPrivate
  | OmpTaskloopReduction
  | OmpNumTasks
  | OmpGrainSize
  | OmpCollapse
  | OmpUntied
  | OmpMergeable
  | OmpNogroup
  | OmpPriority
;

OmpSingleClause:
    OmpPrivate
  | OmpFirstPrivate
;

OmpCriticalClause:
    OmpCriticalName
;

OmpMaskedClause:
    OmpFilter
;
////////////////////////////////////////////////////

////////////////////////////////////////////////////
//              Clauses definitions
////////////////////////////////////////////////////
OmpNumThread: 'num_threads' '(' thread=ThreadIndex ')';
OmpNumTasks: 'num_tasks' '(' tasks=TaskIndex ')';
OmpGrainSize: 'grainsize' '(' tasks=TaskIndex ')';
OmpDefault: 'default' '(' status=OmpDefaultStatus ')';
OmpProcBind: 'proc_bind' '(' status=OmpProcBindStatus ')';
OmpPrivate: 'private' '(' args+=ID[','] ')';
OmpShared: 'shared' '(' args+=ID[','] ')';
OmpFirstPrivate: 'firstprivate' '(' args+=ID[','] ')';
OmpLastPrivate: 'lastprivate' '(' args+=ID[','] ')';
OmpCopyin: 'copyin' '(' args+=ID[','] ')';
OmpReduction: 'reduction' '('op=OmpReductionOperator ':' args+=ID[','] ')';
OmpTaskloopReduction: ctype=OmpTLReductionType '('op=OmpReductionOperator ':' args+=ID[','] ')';
OmpCollapse: 'collapse' '(' n=INT ')';
OmpLinear: 'linear' '(' val=ID ':' step=INT ')';
OmpOrdered: 'ordered' ('(' n=INT ')')?;
OmpSchedule: 'schedule' '(' kind=OmpScheduleKind (',' chunk_size=INT)? ')';
OmpEndClause: 'end' construct=OpenmpConstructs (simd='simd')? (nowait='nowait')?;
OmpCriticalName: '(' args=ID ')';
OmpFilter: name='filter' '(' n=INT ')';
OmpPriority: name='priority' '(' n=INT ')';
OmpUntied: name='untied';
OmpMergeable: name='mergeable';
OmpNogroup: name='nogroup';
////////////////////////////////////////////////////

////////////////////////////////////////////////////
OmpScheduleKind: ('static' | 'dynamic' | 'guided' | 'auto' | 'runtime' );
OmpProcBindStatus: ('master' | 'close' | 'spread');
OmpReductionOperator: ('+' | '-' | '*' | '/');
OmpTLReductionType: ('in_reduction' | 'reduction');
OmpDefaultStatus: ('private' | 'firstprivate' | 'shared' | 'none');
OpenmpConstructs: ('masked' | 'master' | 'single' | 'parallel' | 'for' | 'critical' ('(' ID  ')')?);

ThreadIndex: (ID | INT);
TaskIndex: (ID | INT);
NotaStmt: /.*$/;
////////////////////////////////////////////////////
